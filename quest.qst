:start
polito=0
pln <center><h3>Классические сказки пересказанные Горафом:</h3> <h2>Золушка</h2></center>
pln <center><i>Данное классическое произведение содержит сцены секса, насилия, черной магии, поклонения Люциферу и категорически не рекомендуется для проигрывания лицами не достигшими 18-летнего возраста.</i></center>
pln
pln Это игра для маленьких девочек (достигших 18-летнего возраста) с тремя концовками, моральным выбором и поучительными выводами в конце.
btn 1, Начать
end

:1
pln В некотором тридевятом королевстве, жила-была девушка, мать у которой умерла, а отец женился второй раз, на злобной и сварливой женщине. Та сразу невзлюбила падчерицу, заставляла ее делать всякую черную работу по дому - мыть за собой посуду, читать Библию и застилать постель.
pln В знак протеста, девушка пряталась на чердаке, и сидела на ящике с золой, чтобы ее пожалели. За это ее прозвали "Золушкой". И как бы она не перемазывалась в золе, Золушка все-равно была миленькой, а для девушки это главное.
btn 2, И тут король объявил бал
end

:2
pln Отец Золушки и ее злая мачеха были приглашены в королевский дворец на бал. Золушка тоже хотела поехать. Но злая мачеха ей не разрешила.
pln - Сперва полей 40 розовых кустов, потом вымой за собой все тарелки и собери рис который ты с утра рассыпала! А когда сделаешь все это, ладно, можешь приходить на бал тоже.
pln Золушка надулась и топнула ножкой. Но мачеха уперла руки в бока и уехала с ее папенькой прямо на бал на коляске.
pln Золушка села на порог и горько заплакала.
pln - Ах если бы принц только мог бы меня увидеть, - запричитала она. - Она сразу бы влюбился в меня, и мне не пришлось бы больше застилать постель или мыть за собой посуду!
btn krylso, Да хватит уже этих многословных вступлений! Начать игру наконец!
end

:krylso
pln Золушка сидит на крыльце, смотрит в закат, думает о тяжкой судьбе своей. Слева находится сад с розами.
btn sad, Идти в сад
btn dom, Идти в дом
end

:sad
nepolito=40-polito
pln Золушка в саду где растет 40 розовых кустов. #polito$ из них полито, #nepolito$ не полито.
pln Слева колодец, справа крыльцо дома, чуть поодаль находится сарай.
btn krylso, Идти на крыльцо
btn kolodec, Идти к колодцу
btn sarai, Идти в сарай
if fill==1 then btn pour, Полить розовый куст водой из ведра
end

:pour
inv+ Пустое Ведро
inv- Ведро с водой
fill=0
polito=polito+1
pln Золушка полила розовый куст водой из ведра! Ведро опустело.
if polito==1 then pln Осталось всего 39 розовых кустов! Еще немного поднажать и Золушка сможет отправиться на бал!
if polito==2 then pln - 39 розовых кустов было неполито в саду. Один из них полила Золушка, о йес. Осталось 38 розовых кустов неполитых в саду, - затянула Золушка старую грустную негритянскую рабочую песню, из которой после алкаши-гитаристы сделают вполне себе недурственный блюз.
if polito==3 then goto ending1
if polito>39 then goto ending1_1
btn sad, Дальше
end 

:kolodec
pln Золушка оказалась у небольшого, пусть и старого колодца, выложенного серым и зеленым кирпичом. Вымощенная жовтым дорожка ведет в сад.
btn sad, Идти по жовтой дорожке в сад
if vedro==1 and fill!=1 then btn fillvedro, Наполнить ведро водой из старого колодца
end

:fillvedro
inv- Пустое Ведро
inv+ Ведро с водой
fill=1
pln Золушка наполнила ведро водой из колодца.
btn kolodec, Дальше
end

:sarai
pln Золушка в сарае. Единственный выход отсюда ведет в сад.
if vedro!=1 then pln В сарае имеется старое ведро.
btn sad, Идти в сад
if vedro!=1 then btn vedro, Взять ведро
end

:vedro
pln Глубокоуважаемый читатель считает что взятие ведра приблизит Золушку к балу? Ну ладно, ок, теперь у Золушки есть ведро. 
vedro=1
inv+ Пустое Ведро
btn sarai, Дальше
end

:dom
pln Золушка в прихожей дома, где она вынуждена жить с тех пор как ее отец женился на злой мачехе. Прихожую украшают ветвистые рога. Слева была дверь на кухню. Старая лестница уводила на чердак. Так же множество коридоров и дверей вели в многочисленные комнаты и помещения которые автор не успел реализовать.
btn krylso, Выйти на двор
btn kitchen, Идти на кухню
btn cerdak, Идти на чердак
btn rooms, Идти в прочие многочисленные комнаты и помещения, которые автор не успел реализовать
end 

:cerdak
pln На чердаке нет ничего ценного, кроме ящика с золой.
btn dom, Спуситься вниз
btn yash, Сесть на ящик и плакать
end

:yash
pln Золушка села на ящик с золой и горько заплакала, кляня свою трудную судьбу. Так она делал каждый день, а иногда и по два раза.
btn cerdak, Хватит плакать
end

:kitchen
if uborka==0 then pln На кухне срач, рис рассыпан, посуда не вымыта. Ах да, здесь же завтракала Золушка.
if uborka==1 then pln На кухне чистота и порядок, все блестит.
if uborka==0 then btn uborka, Прибраться на кухне
btn dom, Покинуть кухню
end

:rooms
nepolito=40-polito
pln - Ах у меня нет времени осматривать все эти многочисленные комнаты и помещения, - всплеснула руками Золушка. - Отец мог купить дом и поскромнее для этой козы!
pln У Золушки не было времени осматривать все эти многочисленные комнаты и помещения вовсе не потому что автор не успел реализовать оные, а потому что бал уже начался, а #nepolito$ розовых кустов было еще не полито!
btn dom, Дальше
end

:ending1
pln Я так понимаю моя юная читательница, что Золушка всерьез настроена полить все 40 розовых кустов?
pln Конечно будть я злой Золушкиной мачехой, я бы заставил тебя нажимать 40 раз, но я позволю тебе выполнить всю тяжелую работу нажав лишь одну кнопку.
btn ending1_1, Да, да, я хочу полить все 40 розовых кустов нажатием одной кнопки
btn sad, Немедленно оставьте меня в покое, мне нравится фармить кусты
end

:ending1_1
pln Золушка полила все кусты, прибралась в доме, переделала всю работу и вытерла пот со лба. Уже стемнело.
pln - Наконец-то я смогу пойти на бал, - сказала она.
pln Но тут раздался стук колес кареты, из которой вышли ее отец и ее злая мачеха. Бал уже закончился.
pln Злая мачеха похвалила бедную Золушку за проделанную работу. Вскоре Золушку выдали замуж за соседского кулака, которому нужна была работящая жена.
pln Целыми днями Золушка трудилась по хозяйству, кормила поросят, поливала цветы и консервировала абрикосы, а вечерами, с мужем, они поросят с абрикосами жрали. Жили они не очень то счастливо, и умерли в один день. От скуки.
pln А на бал Золушка так ни разу и не попала, и принц ее так и не увидел, как и она принца.
pln Мораль: Лучше сделать и жалеть, чем не сделать и жалеть
pln А ты как думаешь, моя юная читательница?
pln КОНЕЦ
end

:uborka
pln Золушка постучала по кастрюлям и всплеснула руками:
pln - Ах, если бы сюда явилась моя фея крестная, и взмахнув волшебной палочкой убрала все это! Тогда я могла бы отправиться на бал и увидеть принца, а он сразу бы влюбился в меня!
pln И только она вымолвила все это, как за ее спиной раздался громкий хлопок и в воздухе запахло серой. Золушка обернулась, и увидела стройного мужчину в узких полосатых штанах плотно облегающих причинное место, розовой майке с жабой и надписью на иностранном языке и элегантной мужской сумочкой через плечо.
pln - Ты фея крестная? - удивилась Золушка.
pln - Знаешь ли, я уже не фея крестная, - ответил тот. - Я впрочем, вообще не фея крестная. А Люцифер.
pln Сказав это он ткнул пальцем в свою розовую майку, где так и было написано "I'm Lucifer, baby".
pln - И ты хочешь чтобы я продала тебе свою душу? - начала торговаться Золушка.
pln - К чему мне это? - улыбнулся Люцифер. - Души девушек что не моют за собой посуду и так мои. Но ты сможешь попасть на бал, если позволишь себе помочь. Раздевайся.
pln - Но... я... - застеснялась Золушка.
pln - Раздевайся, раздевайся, - продолжал Люцифер. - Не можешь же ты поехать на бал в этом грязном и ужасном платье. Скромность не для тех, кто хочет поехать на бал и понравится принцу.
btn agree, Раздеться
btn disagree, Остаться скромницей
end

:disagree
pln - Как пожелаете, барышня, - Люцифер сделал книксен и растворился в воздухе.
pln Золушке не оставалось ничего другого, как принятся за уборку и полив.
btn ending1_1, Дальше
end

:agree
pln - Ладно, - покраснела Золушка и сбросила свое домашнее платье, представ перед Люцифером в чем мать родила.
pln Тот лукаво улыбнулся и взмахнул волшебной палочкой. Тут же девушку окружил волшебные искры, и оказалась что она одета в чудесное бальное платье.
pln - Ух ты, - воскликнула она. - Какое красивое платье. Невесомое, я будто голая.
pln Золушка попробовала было ухватится за рукав, но ее пальцы прошли сквозь ткань.
pln - А ты и есть голая, - ответил Люцифер. - И не надо делать такое лицо. В стране кризис, непросто достать настоящее платье. Просто не разрешай никому кроме принца до тебя дотрагиватся.
pln - А если принц что-то заподорит? - испугалась Золушка.
pln - Просто прижмись к нему грудью, - посоветовал Люцифер. - Поверь, он не будет против, даже если все поймет. Уж я его знаю.
pln Люцифер критически оглядел девушку со всех сторон, от чего та густо покраснела, и вздохнул:
pln - А вот лабутены тебе нужны будут настоящие, чтоб поднять и выгодно подчеркнуть бедра и зад. Ну в этом нам поможет MasterCard.
pln И достал из воздуха пару туфель лодочек томнокремового оттенка. Девушка поняла из сказанного только что у нее неплохие берда и зад, но туфли ей понравились.
pln - Карета подана, - сказал наконец Люцифер и указал Золушке на телегу с сеном.
pln - Это вовсе не похоже на карету, - испугалась та.
pln - Тут как с платьем, - рассмеялся Люцифер, щелкнул пальцами, и телега превратилась в модную карету. Люцифер запрыгнул на козлы и повез Золушку на бал.
btn ending2, Я не хочу столько всего читать, можно мне просто взять и нажать куда-то
end

:ending2
pln Когда карета остановилась у дворца, Люцифер тихонько шепнул Золушке на ухо:
pln - Кстати, и платье, и карета исчезнут в полночь, с двенадцатым ударом часов. Так что будь добра, поторопись.
pln Сказав это он исчез куда-то и сам.
pln Золушка произвела на балу фурор. Принц обратил на нее внимание и тут же пригласил танцевать. Однако как только он взял ее за талию, и скользнул рукой чуть ниже спины, как глаза его расширились от удивления. Золушка тут же прижалась к нему грудью, несмотря на то что была кадриль, и принц сильно раскраснелся, начал тяжело дышать, в бедро Золушке уперлось что-то твердое... словом девушке пришлось вывести его на балкон подышать.
pln - Я люблю вас, - сказал принц. - Отдайтесь мне прямо сейчас. Вон там двери спальни.
pln "Я не такая", подумала Золушка, но тут часы начали бить двенадцать часов, и Золушка поняла, что сейчас ее платье исчезнет, и она останется прямо посреди бала голой. С перепугу она оттолкнула принца и бросилась бежать.
btn ending3, Бежать домой вниз по лестнице
btn ending2_2, Бежать в ту самую спальню
end

:ending2_2
pln Как только Золушка вбежала в спальню, часы пробили полночь, и платье ее исчезло. Через секунду за ней в комнату вбежал принц, и тут же овладел девушкой.
pln Утром Золушка никак не могла объяснить куда подевалась ее платье, но принц был добр и приказал сшить ей новое, а также казнить злую мачеху Золушки.
pln А после сыграли сразу две свадьбы - Золушку принц выдал замуж за старого графа Препиньяка, а сам женился на принцессе Амалии. Но каждую субботу принц отправлялся на охоту, а сам заезжал к Золушке и проводил в ее спальне все выходные. А муж Золушка, граф Препиньяк, подглядывал за ними через специально устроенную щель.
pln Так жили они все втроем долго и счастливо, и умерли в один день - от яда подсыпаного злой принцессой Амалией - но это уже другая история.
pln Мораль: Все хорошо, что хорошо кончается
pln А ты как думаешь, моя юная читательница?
pln КОНЕЦ
end

:ending3
pln Как только Золушка выбежала из дворца, платье ее исчезло, каблук сломался, начался дождь.
pln Домой девушка добралась лишь под утро. Там злая мачеха, увидев Золушку заявившеюся домой без одежды и в одном туфле, побила девушку веником и отдала в монастырь, где она, сохранившая свои чудесные принципы, и прожила в чистоте и невинности до самой смерти.
pln Мораль: Не так важно, осталась ли ты невинной на самой деле, как важно то, как это выглядит со стороны
pln А ты как думаешь, моя юная читательница?
pln КОНЕЦ
end
